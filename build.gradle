plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

group = 'com.sprint.mission'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // https://mvnrepository.com/artifact/io.netty/netty-resolver-dns-native-macos
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.2.2.Final:osx-aarch_64'

    // logging
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.apache.logging.log4j:log4j-layout-template-json:2.20.0'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'org.apache.logging.log4j:log4j-jul' // Tomcat log

    // RSS/XML parsing
    implementation 'com.rometools:rome:2.1.0'
    implementation 'com.rometools:rome-modules:2.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // 재시도, RateLimiter(에: API 요청 개수 제한) 등 다양한 내결함 제어릃 지원하는 경량 라이브러리
    implementation 'dev.failsafe:failsafe:3.3.2'

    // Swagger UI 활성화
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'

    // Lombok과 MapStruct를 동시에 사용하는 경우 Lombok이 제대로 동작하지 않는 문제. MapStruct 위에 배치한다.
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // AWS S3 SDK 의존성 추가
    implementation 'software.amazon.awssdk:s3:2.31.7'
    // dotenv-java 의존성 추가
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // Apache Commons Text 의존성 추가
    implementation 'org.apache.commons:commons-text:1.13.1'

    // Metrics
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // DB
    implementation 'org.postgresql:postgresql:42.7.5'
    runtimeOnly 'com.h2database:h2'

    // Querydsl 설정
    // https://mvnrepository.com/artifact/com.querydsl/querydsl-jpa
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:3.1.0"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2'
    testImplementation 'org.testcontainers:mongodb:1.19.7'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.github.cdimascio:dotenv-java:3.0.2'
    testImplementation 'org.springframework.batch:spring-batch-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// QueryDSL 설정
def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
    main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean {
    delete file(querydslDir)
}
